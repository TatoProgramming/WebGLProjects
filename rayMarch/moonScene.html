<!DOCTYPE html>
<html>
<style type="text/css">
    body {
        background: black;
        /*margin: auto;*/
        /*width: 50%;*/
    }
</style>

<script id="vshader" type="x-shader/x-vertex">
    attribute  vec4 vPosition;

    varying vec4 fPosition;
    void main(){
        gl_Position = vPosition;
    }
</script>

<script id="fshader" type="x-shader/x-fragment">
    precision highp float;
    #define PI 3.14159265359
    varying vec2 fTexCoord;
    uniform float time;
    uniform vec2 resolution;
    uniform int mode;

    const int MAX_MARCHING_STEPS = 164;
    const float MAX_DIST = 250.0;
    const float MIN_DIST = 0.001;

    struct LightSource{
        vec3 pos;
        vec3 La;
        vec3 Ld;
        vec3 Ls;
    };

    LightSource redLight = LightSource(
        vec3(0.0, 16.0, -57.5),
        vec3(0.0, 0.0, 0.0),
        vec3(0.8, 0.0, 0.0),
        vec3(0.8, 0.0, 0.0)
    );

    const LightSource light1 = LightSource(
        vec3(0.0, 150.0, -900.0),
        vec3(0.1, 0.1, 0.1),
        vec3(1.0, 1.0, 1.0),
        vec3(1.0, 1.0, 1.0)
    );

    const LightSource light2 = LightSource(
        vec3(0.0, 1000.0, 0.0),
        vec3(0.0, 0.0, 0.0),
        vec3(46.0/255.0, 90.0/255.0, 91.0/255.0),
        vec3(0.0, 0.0, 0.0)
    );

    float smin( float a, float b, float k ){
        float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
        return mix( b, a, h ) - k*h*(1.0-h);
    }
    // ------------- PRIMITIVES ------------------
    // These distance functions are take from http://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm
    // Who has a ton of great resources and tutorials on ray marching
    float sphere(vec3 point, float radius){
        return length(point) - radius;
    }

    float plane(vec3 point){
        return dot(point, vec3(0.0,1.0,0.0)) + 0.5;
    }

    //	Classic Perlin 3D Noise
    //	by Stefan Gustavson
    //
    vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
    vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}
    vec3 fade(vec3 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}

    float cnoise(vec3 P){
      vec3 Pi0 = floor(P); // Integer part for indexing
      vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
      Pi0 = mod(Pi0, 289.0);
      Pi1 = mod(Pi1, 289.0);
      vec3 Pf0 = fract(P); // Fractional part for interpolation
      vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
      vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
      vec4 iy = vec4(Pi0.yy, Pi1.yy);
      vec4 iz0 = Pi0.zzzz;
      vec4 iz1 = Pi1.zzzz;

      vec4 ixy = permute(permute(ix) + iy);
      vec4 ixy0 = permute(ixy + iz0);
      vec4 ixy1 = permute(ixy + iz1);

      vec4 gx0 = ixy0 / 7.0;
      vec4 gy0 = fract(floor(gx0) / 7.0) - 0.5;
      gx0 = fract(gx0);
      vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
      vec4 sz0 = step(gz0, vec4(0.0));
      gx0 -= sz0 * (step(0.0, gx0) - 0.5);
      gy0 -= sz0 * (step(0.0, gy0) - 0.5);

      vec4 gx1 = ixy1 / 7.0;
      vec4 gy1 = fract(floor(gx1) / 7.0) - 0.5;
      gx1 = fract(gx1);
      vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
      vec4 sz1 = step(gz1, vec4(0.0));
      gx1 -= sz1 * (step(0.0, gx1) - 0.5);
      gy1 -= sz1 * (step(0.0, gy1) - 0.5);

      vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
      vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
      vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
      vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
      vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
      vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
      vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
      vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

      vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
      g000 *= norm0.x;
      g010 *= norm0.y;
      g100 *= norm0.z;
      g110 *= norm0.w;
      vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
      g001 *= norm1.x;
      g011 *= norm1.y;
      g101 *= norm1.z;
      g111 *= norm1.w;

      float n000 = dot(g000, Pf0);
      float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
      float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
      float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
      float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
      float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
      float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
      float n111 = dot(g111, Pf1);

      vec3 fade_xyz = fade(Pf0);
      vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
      vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
      float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
      return 2.2 * n_xyz;
    }

    float dispSphere(vec3 point, float radius){
        float sd = plane(point);
        float c3 = 0.0;
        c3 = 0.2*cnoise(0.2*vec3(point.xz, time*3.0));
        return sd + c3;
    }

    float sdCapsule( vec3 p, vec3 a, vec3 b, float r ){
        vec3 pa = p - a, ba = b - a;
        float h = clamp(dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
        return length( pa - ba*h ) - r;
    }

    float udRoundBox( vec3 p, vec3 b, float r ){
        return length(max(abs(p)-b,0.0))-r;
    }

//    float sdBox( vec3 p, vec3 b ){
//      vec3 d = abs(p) - b;
//      return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
//    }

    // ---------- End of PRIMITIVES --------------

//    vec3 tran(vec3 point, vec3 trans){
//        vec4 nPos = vec4(point, 1.0);
//        vec4 res = nPos * mat4(
//            1.0, 0.0, 0.0, trans.x,
//            0.0, 1.0, 0.0, trans.y,
//            0.0, 0.0, 1.0, trans.z,
//            0.0, 0.0, 0.0, 1.0
//        );
//        return res.xyz;
//    }
//
//    mat3 rotateX(vec3 point, float angle){
//        float c = cos(angle);
//        float s = sin(angle);
//        mat3 rot = mat3(
//            1., 0.0, 0.0,
//            0.0, c, -s,
//            0.0, s, c
//        );
//        return rot;
//    }

    mat3 rotateZ(vec3 point, float angle){
        float c = cos(angle);
        float s = sin(angle);
        mat3 rot = mat3(
            c, -s, 0.0,
            s, c, 0.0,
            0.0, 0.0, 1.0
        );
        return rot;
    }
//
//    mat3 rotateY(vec3 point, float angle){
//        float c = cos(angle);
//        float s = sin(angle);
//        mat3 rot = mat3(
//            c, 0.0, s,
//            0.0, 1.0, 0.0,
//            -s, 0.0, c
//        );
//        return rot;
//    }

    float bouy(vec3 p){
        vec3 point = rotateZ(p, sin(time/2.0)/8.0)*p;
//        redLight.pos.x = redLight.pos.x + (0.04*-sin(time/2.));

        float k = 1.5;
        float d = sphere(point - vec3(0.0, -1.0, -60.0), 5.0);
        d = smin(d ,sphere(point - vec3(0.0, 13.5, -57.5), 0.5), 0.5);
        d = smin(d, sdCapsule(point - vec3(3.0, 2.0, -57.5), vec3(0.0, 0.0, 0.0), vec3(-1.0, 9.0, 0.0), 0.5), k);
        d = smin(d, sdCapsule(point - vec3(-3.0, 2.0, -57.5), vec3(0.0, 0.0, 0.0), vec3(1.0, 9.0, 0.0), 0.5), k);
//        d = smin(d, sdCapsule(point - vec3(3.0, 2.0, -58.5), vec3(0.0, 0.0, 0.0), vec3(-1.0, 9.0, 0.0), 0.5), k);
//        d = smin(d, sdCapsule(point - vec3(-3.0, 2.0, -58.5), vec3(0.0, 0.0, 0.0), vec3(1.0, 9.0, 0.0), 0.5), k);
        d = smin(d, udRoundBox(point - vec3(0.0, 12.0, -57.5), vec3(2.5, 0.5, 2.0), 0.5), k);
        return d;
    }

    vec2 minOp(vec2 d1, vec2 d2){
        if(d1.x < d2.x){
            return d1;
        }
        return d2;
    }

    vec2 sceneMap(vec3 point){
        vec2 d = vec2(100000.0,-1.0);
        d = minOp(d, vec2(dispSphere(point, 1.0), 1.0));
        d = minOp(d, vec2(sphere(point - (vec3(0.0, 50.0, -250.0)), 20.0), 2.0));
        d = minOp(d, vec2(bouy(point), 3.0));
        return d;
    }

//    float volumeMap(vec3 point){
//        float d = 100000.0;
//        d = min(d, dispSphere(point - redLight.pos, 4.0));
//        return d;
//    }

    // takes this http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
    // this is done by taking the gradient
    vec3 normal(vec3 p){
        return normalize(vec3(
            sceneMap(vec3(p.x + MIN_DIST, p.y, p.z)).x - sceneMap(vec3(p.x - MIN_DIST, p.y, p.z)).x,
            sceneMap(vec3(p.x, p.y + MIN_DIST, p.z)).x - sceneMap(vec3(p.x, p.y - MIN_DIST, p.z)).x,
            sceneMap(vec3(p.x, p.y, p.z + MIN_DIST)).x - sceneMap(vec3(p.x, p.y, p.z - MIN_DIST)).x
        ));
    }

    vec3 diffuse(vec3 kd, vec3 n, vec3 l, vec3 Ld){
        return kd*Ld*max(dot(n,l), 0.0);
    }

    vec3 specular(vec3 ks, vec3 n, vec3 l, vec3 v, float shinyness, vec3 Ls){
        vec3 r = normalize(reflect(-l, n));
        return ks*Ls*pow(max(dot(r,v), 0.0), shinyness);
    }

    vec3 phong(vec3 ka, vec3 kd, vec3 ks, vec3 point, vec3 eye, vec3 n, LightSource lightSource, float shinyness){
        vec3 l = normalize(lightSource.pos - point); // this seems wrong
        vec3 v = normalize(eye - point);
        vec3 Ia = ka*lightSource.La;
        vec3 Id = diffuse(kd, n, l, lightSource.Ld);
        vec3 Is = specular(ks, n, l, v, shinyness, lightSource.Ls);

        return Ia + Id + Is;
    }

    // used this as a guide http://blog.hvidtfeldts.net/index.php/2011/06/distance-estimated-3d-fractals-part-i/
    vec2 march(vec3 rayOr, vec3 rayDir, out int steps){
        float totalDist = 0.0;
        steps = 0;
        float materialID = -1.0;
        for(int i = 0; i < MAX_MARCHING_STEPS; i++){
            vec3 p = rayOr + totalDist * rayDir;
            vec2 result = sceneMap(p);

            if(result.x < MIN_DIST){
                break;
            }
            totalDist += result.x;
            materialID = result.y;

            if(totalDist >= MAX_DIST){
                return vec2(MAX_DIST, -1.0);
            }
            steps++;
        }
        return vec2(totalDist, materialID);
    }

    mat3 lookAt(vec3 eye, vec3 at, vec3 up){
       vec3 n = normalize(at - eye);
       vec3 u = normalize(cross(up, n));
       vec3 v = normalize(cross(n, u));
       return mat3(u,v,n);
    }

    // Idea from http://www.iquilezles.org/www/articles/fog/fog.htm
    vec3 applyFog(vec3 color, vec3 fogColor, float distance, vec3 rayDir){
        float amount = 1.0 - exp(-distance*0.01);
        return mix(color, fogColor, amount);
    }

    float shadow(vec3 point, LightSource lightSource){
        vec3 rayOr = point;
        vec3 rayDir = normalize(lightSource.pos - point);
        float totalDist = 3.5;
        float res = 1.0;
        for(int i = 0; i < 20; i++){
            vec3 p = rayOr + totalDist *rayDir;
            res = sceneMap(p).x;
            totalDist += res;
            if(res < MIN_DIST || totalDist > 50.0){
                break;
            }
        }
        return clamp(res, 0.0, 1.0);
    }


//    float calcVolume(vec3 point, vec3 rayDir){
//        vec3 rayOr = point;
//        float totalDist = 0.0;
//        float absorbedLight = 0.0;
//        float stepSize = 1.0/32.0;
//        for(int i = 0; i < 32; i++){
//            vec3 p = rayOr + stepSize * rayDir;
//            float result = volumeMap(p);
//            if(result < -4.1){
//                absorbedLight += stepSize;
//            }
//            totalDist += stepSize;
//        }
//        return absorbedLight;
//    }

    void main() {

        vec3 seaAmb = vec3(0.0,0.0,0.0);
        vec3 seaDif = vec3(13.0/255.0, 35.0/255.0, 58.0/255.0);
        vec3 seaSpec = vec3(1.0, 1.0, 1.0);

        vec3 bouyAmb = vec3(0.1, 0.1, 0.1);
        vec3 bouyDif = vec3(0.0, 0.4, 1.0);
        vec3 bouySpec = vec3(1.0, 1.0, 1.0);


        vec3 color = vec3(0.2);
        vec3 lightPos = (vec3(0.0, 150.0, -1000.0));
        vec3 lightPos2 = vec3(0.0, 1000.0, 0.0);

        vec2 xy = gl_FragCoord.xy/resolution.xy;
        xy = xy*2.0 - 1.0;
        xy /= resolution.x/resolution.y;

        vec3 rayOr = vec3(0.0, 10.0, 10.0);
        mat3 lookAtRes = lookAt(rayOr, vec3(0.0,0.0,-100.0), vec3(0.0,1.0,0.0));

        int steps = 0;
        float d = MAX_DIST;
        vec3 point = vec3(0.0);
        vec2 obj = vec2(-1.0);
        vec3 totalColor = vec3(0.0,0.0,0.0);

        vec2 pixelPos = xy;
        // Anti-Aliasing
        const int aa = 5;
        float offset = MIN_DIST - 0.0003;
        for(int i = 0; i < aa; i++){
            // TODO: figure out how to do this without if statements
            if(i == 1){
                xy.y = pixelPos.y + offset;
                xy.x = pixelPos.x + offset;
            }else if(i == 2){
                xy.y = pixelPos.y - offset;
                xy.x = pixelPos.x - offset;
            }else if(i == 3){
                xy.y = pixelPos.y + offset;
                xy.x = pixelPos.x - offset;
            }else if(i == 4){
                xy.y = pixelPos.y - offset;
                xy.x = pixelPos.x + offset;
            }

            vec3 rayDir = normalize(vec3(xy, 2.0))*lookAtRes;
            obj = march(rayOr, rayDir, steps);
            point = rayOr + obj.x * rayDir;
            vec3 n = normal(point);
            vec3 iterationColor = vec3(0.05, 0.05, 0.05);


            if(obj.x < MAX_DIST - MIN_DIST){
                if(obj.y == 1.0){
                    vec3 color1 = phong(
                        seaAmb, seaDif, seaSpec,
                        point, rayOr, n,
                        light1, 100.0
                    );
                    iterationColor = mix(vec3(0.0), color1, shadow(point, light1));

                }
                if(obj.y == 2.0){
                    iterationColor = vec3(1.0, 1.0, 1.0);
                }
                if(obj.y == 3.0){
                    iterationColor = phong(
                        bouyAmb, bouyDif, bouySpec,
                        point, rayOr, n,
                        light1, 100.0
                    );
                }
            }

            totalColor += applyFog(iterationColor,
                vec3(0.7), //todo: pull this out as fog color
                obj.x,
                rayDir
            );
        }
        totalColor /= float(aa);
        color = totalColor;
        gl_FragColor = vec4(color, 1.0);
    }


</script>

<script type="text/javascript" src="./common/webgl-utils.js"></script>
<script type="text/javascript" src="./common/initShaders.js"></script>
<script type="text/javascript" src="./common/MV.js"></script>
<script type="text/javascript" src="fractal.js"></script>
<body>
<canvas id="gl-canvas" width="900" height="900">
    Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<br>
</body>
</html>