<!DOCTYPE html>
<html>
<style type="text/css">
    body {
        background: black;
        /*margin: auto;*/
        /*width: 50%;*/
    }
</style>

<script id="vshader" type="x-shader/x-vertex">
    attribute  vec4 vPosition;

    varying vec4 fPosition;
    void main(){
        gl_Position = vPosition;
    }
</script>

<script id="fshader" type="x-shader/x-fragment">
    precision highp float;
    #define FREQ_ARR_SIZE 512
    #define PI 3.14159265359
    varying vec2 fTexCoord;
    uniform float time;
    uniform vec2 resolution;
    uniform int mode;

    const int MAX_MARCHING_STEPS = 255;
    const float MAX_DIST = 400.0;
    const float MIN_DIST = 0.001;
    vec3 La = vec3(0.1, 0.1, 0.1);
    vec3 Ld = vec3(1.0, 1.0, 1.0);
    vec3 Ls = vec3(1.0, 1.0, 1.0);


    // ------------- PRIMITIVES ------------------
    // These distance functions are take from http://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm
    // Who has a ton of great resources and tutorials on ray marching
    float sphere(vec3 point, float radius){
        return length(point) - radius;
    }

    float plane(vec3 point){
        return dot(point, vec3(0.0,1.0,0.0)) + 0.5;
    }

    float sdBox( vec3 p, vec3 b ){
      vec3 d = abs(p) - b;
      return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
    }

    // made this by mistake a octahedron
    float octahedron(vec3 point){
        return (
            max(
                max(
                    max(-point.x + point.y - point.z, -point.x + point.y + point.z),
                    max(point.x - point.y + point.z, point.x - point.y - point.z)
                ),
                max(
                    max(point.x + point.y - point.z, point.x + point.y + point.z),
                    max(-point.x - point.y - point.z, -point.x - point.y + point.z)
                )
            ) - 1.0)/sqrt(3.0);
    }

    // tetrahedron is four planes
    float tetrahedronDist(vec3 point){
        return (
            max(
                max(-point.x - point.y - point.z, point.x + point.y - point.z),
                max(-point.x + point.y + point.z, point.x - point.y + point.z)
            ) - 1.0)/sqrt(3.0);
    }
    // ---------- End of PRIMITIVES --------------

    vec3 rotateX(vec3 point, float angle){
        float c = cos(angle);
        float s = sin(angle);
        mat3 rot = mat3(
            1, 0.0, 0.0,
            0.0, c, -s,
            0.0, s, c
        );
        return point * rot;
    }


    vec3 rotateZ(vec3 point, float angle){
        float c = cos(angle);
        float s = sin(angle);
        mat3 rot = mat3(
            c, -s, 0.0,
            s, c, 0.0,
            0.0, 0.0, 1.0
        );
        return point * rot;
    }

    vec3 rotateY(vec3 point, float angle){
        float c = cos(angle);
        float s = sin(angle);
        mat3 rot = mat3(
            c, 0.0, s,
            0.0, 1.0, 0.0,
            -s, 0.0, c
        );
        return point * rot;
    }

    // following this thread for information on how to do interesting fractals
    //http://www.fractalforums.com/sierpinski-gasket/kaleidoscopic-(escape-time-ifs)/?PHPSESSID=aa13169ea748515f02a55a295ecab2c6
    float sierpinskiTriangle(vec3 point){
        float scale = 2.0;
        int n = 0;
        for(int i = 0; i < 6; i++){
            point = point.xyz;
            if(point.x + point.y < 0.0)
                point.yx = -point.xy;
            if(point.x + point.z < 0.0)
                point.zx = -point.xz;
            if(point.y + point.z < 0.0)
                point.yz = -point.zy;

            point = point*scale - 1.0;
            n++;
        }
        return tetrahedronDist(point) * pow(scale, -float(n));
    }

    // This is taken from http://www.fractalforums.com/programming/ray-marching-distance-functions/
    float mengerSponge(vec3 point){
        float scale = 3.0;

        int n = 0;
        for(int i = 0; i < 8; i++){
            point.x = abs(point.x);
            point.y = abs(point.y);
            point.z = abs(point.z);

            if(point.x < point.y){
                point.xy = point.yx;
            }
            if(point.y < point.z){
                point.yz = point.zy;
            }
            if(point.x < point.y){
                point.xy = point.yx;
            }
            point = point*scale - 2.0;
            if(point.z < -1.0) point.z += 2.0;
            n++;
        }
        return (sphere(point, 1.5))* pow(scale, -float(n));
    }

    // messing around with folds trying to figure them out and stumbled onto this fella
    float messingWithFolds(vec3 point){
        float scale = 2.0;
        int n = 0;
        for(int i = 0; i < 9; i++){
            point = point.xyz;
            if(point.x + point.y < 0.0)
                point.yx = -point.xy;
            if(point.x + point.z < 0.0)
                point.zx = -point.xz;
            if(point.y + point.z < 0.0)
                point.yz = -point.zy;
            point = point.xzy;

            point.x = point.x*scale - 0.8;
            point.y = -abs(point.y);
            point.z = point.z*scale;
            if(point.z > 1.0) point.z -= 2.0;

            n++;
        }
        return (length(point) - float(n)/scale) * pow(scale, -float(n)); // scale the primitive by the number of steps
    }

    // this is taken from http://blog.hvidtfeldts.net/index.php/2011/09/distance-estimated-3d-fractals-v-the-mandelbulb-different-de-approximations/
    float mandelbulb(vec3 point) {
       float power;
       if(mode == 5){
            power = 6.0;
       }else{
            power = mod(time/3.0, 20.0);
       }
       vec3 z = point;
       float dr = 1.0;
       float r = 0.0;
       for(int i = 0; i < 20; i++){
            r = length(z);
            if(r > 2.0) break;

            float theta = acos(z.z/r);
            float phi = atan(z.y/z.x);
            dr = pow(r, power - 1.0) * power * dr + 1.0;

            float zr = pow(r, power);
            theta = theta*power;
            phi = phi*power;

            z = zr * vec3(sin(theta)*cos(phi), sin(phi)*sin(theta), cos(theta));
            if(mode == 1){
                z += point*2.0;
            } else {
                z += point;
            }
       }

       return 0.5*log(r)*r/dr;
    }

    float sceneMap(vec3 point) {
        float d = 100000.0;

        point = rotateY(point, time/4.0);
        if(mode == 0 || mode == 1){
            d = min(d, mandelbulb(point));
        }else if(mode == 2){
            point = rotateX(point, PI/3.2);
            point = rotateY(point, PI/4.0);
            d = min(d, sierpinskiTriangle(point));
        }else if(mode == 3){
            d = min(d, messingWithFolds(point));
        }else if(mode == 4){
            d = min(d, mengerSponge(point));
        }else if(mode == 5){
            d = min(d, mengerSponge(point));
            d = max(d, mandelbulb(point));
        }
        return d;
    }

    // takes this http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
    // this is done by taking the gradient
    vec3 normal(vec3 p){
        return normalize(vec3(
            sceneMap(vec3(p.x + MIN_DIST, p.y, p.z)) - sceneMap(vec3(p.x - MIN_DIST, p.y, p.z)),
            sceneMap(vec3(p.x, p.y + MIN_DIST, p.z)) - sceneMap(vec3(p.x, p.y - MIN_DIST, p.z)),
            sceneMap(vec3(p.x, p.y, p.z + MIN_DIST)) - sceneMap(vec3(p.x, p.y, p.z - MIN_DIST))
        ));
    }

    vec3 diffuse(vec3 kd, vec3 n, vec3 l){
        return kd*Ld*max(dot(n,l), 0.0);
    }

    vec3 specular(vec3 ks, vec3 n, vec3 l, vec3 v, float shinyness){
        vec3 r = normalize(reflect(-l, n));
        return ks*Ls*pow(max(dot(r,v), 0.0), shinyness);
    }

    vec3 phong(vec3 ka, vec3 kd, vec3 ks, vec3 lightPos, vec3 point, vec3 eye, vec3 n){
        vec3 l = normalize(lightPos - point); // this seems wrong
        vec3 v = normalize(eye - point);
        vec3 Ia = ka*La;
        vec3 Id = diffuse(kd, n, l);
        vec3 Is = specular(ks, n, l, v, 50.0);

        return Ia + Id + Is;
    }


    // used this as a guide http://blog.hvidtfeldts.net/index.php/2011/06/distance-estimated-3d-fractals-part-i/
    float march(vec3 rayOr, vec3 rayDir, out int steps){
        float totalDist = 0.0;
        steps = 0;
        for(int i = 0; i < MAX_MARCHING_STEPS; i++){
            vec3 p = rayOr + totalDist * rayDir;
            float dist = sceneMap(p);
            if(dist < MIN_DIST){
                break;
            }
            totalDist += dist;
            if(totalDist >= MAX_DIST){
                return MAX_DIST;
            }
            steps++;
        }
        return totalDist;
    }

    mat3 lookAt(vec3 eye, vec3 at, vec3 up){
       vec3 n = normalize(at - eye);
       vec3 u = normalize(cross(up, n));
       vec3 v = normalize(cross(n, u));
       return mat3(u,v,n);
    }

    float shadow(vec3 point, vec3 lightPos){
        vec3 rayOr = point;
        vec3 rayDir = normalize(lightPos - point);
        float totalDist = 0.02;
        float res = 1.0;
        for(int i = 0; i < 20; i++){
            vec3 p = rayOr + totalDist *rayDir;
            res = sceneMap(p);
            totalDist += res;
            if(res < MIN_DIST || totalDist > 50.0){
                break;
            }
        }
        return clamp(res, 0.0, 1.0);
    }

    void main() {

        vec3 color = vec3(0.2);
        vec3 lightPos = (vec3(0.0, 10.0, 10.0));
        vec2 xy = gl_FragCoord.xy/resolution.xy;
        xy = xy*2.0 - 1.0;
        xy /= resolution.x/resolution.y;

        vec3 rayOr = vec3(0.0, 0.0, 2.5);
        vec3 rayDir = normalize(vec3(xy, -1.0));
        int steps = 0;
        float d = MAX_DIST;
        vec3 point = vec3(0.0);

        vec2 obj = vec2(-1.0);

        vec3 totalColor = vec3(0.0);
        vec2 pixelPos = xy;
        float offset = MIN_DIST - 0.0003;

        const int aa = 5;
        vec3 background = vec3(0.3);
        for(int i = 0; i < aa; i++){
            if(i == 1){
                xy.y = pixelPos.y + offset;
                xy.x = pixelPos.x + offset;
            }else if(i == 2){
                xy.y = pixelPos.y - offset;
                xy.x = pixelPos.x - offset;
            }else if(i == 3){
                xy.y = pixelPos.y + offset;
                xy.x = pixelPos.x - offset;
            }else if(i == 4){
                xy.y = pixelPos.y - offset;
                xy.x = pixelPos.x + offset;
            }

            rayDir = normalize(vec3(xy, -1.0));
            d = march(rayOr, rayDir, steps);
            point = rayOr + d * rayDir;
            vec3 iterationColor = vec3(0.0);

            if(d < MAX_DIST){
                float fakeOcc = 1.0 - (float(steps*2)/float(MAX_MARCHING_STEPS));
                vec3 n = normal(point);
                // MandelBulb
                if(mode == 0 || mode == 1){
                    iterationColor = phong(
                        fakeOcc * vec3(0.0, 4.0, 0.0),
                        vec3(0.1, 0.3, 1.0),
                        vec3(0.5, 0.0, 0.0),
                        lightPos,
                        point,
                        rayOr,
                        n
                    );
                // Sierpinski tetrahedron
                }else if(mode == 2){
                    iterationColor += fakeOcc * phong(
                        vec3(0.5, 0.5, 0.5),
                        vec3(0.2, 0.2, 0.5),
                        vec3(0.1, 0.1, 0.3),
                        lightPos,
                        point,
                        rayOr,
                        n
                    );
                    // messing with folds
                }else if(mode == 3){
                    vec3 dif = vec3(0.5, 0.8, 0.9);
                    float l = length(point);
                    iterationColor += phong(
                        fakeOcc * vec3(1.0, 1.0, 1.0),
                        dif*l/(d*d),
                        vec3(0.3, 0.3, 0.3),
                        lightPos,
                        point,
                        rayOr,
                        n
                    );
                // menger Sponge
                }else if(mode == 4){
                   vec3 dif = vec3(0.2,0.8,0.5);
                   float l = length(point);
                   iterationColor += fakeOcc * phong(
                       vec3(1.0, 1.0, 1.0),
                       dif,
                       vec3(0.5, 0.5, 0.5),
                       lightPos,
                       point,
                       rayOr,
                       n
                   );
               }else if(mode == 5){
                // rotten Sponge
                    vec3 dif = vec3(0.0, 0.6, 0.2);
                    float l = length(point);
                    iterationColor += fakeOcc * phong(
                        vec3(0.4, 0.0, 0.7),
                        dif,
                        vec3(1.0, 1.0, 1.0),
                        lightPos,
                        point,
                        rayOr,
                        n
                    );
                }
                iterationColor = mix(iterationColor/2.5, iterationColor, shadow(point, lightPos));
            }else{
                iterationColor += background;
            }
            totalColor += iterationColor;
        }
        totalColor /= float(aa);
        color = totalColor;

        gl_FragColor = vec4(color, 1.0);
    }


</script>

<script type="text/javascript" src="./common/webgl-utils.js"></script>
<script type="text/javascript" src="./common/initShaders.js"></script>
<script type="text/javascript" src="./common/MV.js"></script>
<script type="text/javascript" src="fractal.js"></script>


<body>

<label for="modelSelect">Select Pattern</label>
<select name="select" id="modelSelect" onchange="changeMode(this)">
    <option value="2">Sierpinski Tetrahedron</option>
    <option value="4">Menger Sponge</option>
    <option value="5">Rotten Menger Sponge    </option>
    <option value="3">Random Structure</option>
    <option value="0">MandelBulb</option>
    <option value="1">MandelSpores</option>
</select>
<canvas id="gl-canvas" width="900" height="900">
    Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
<br>
</body>
</html>
